plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.revfactory'
version = '0.1.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jsoup:jsoup:1.15.3'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = 'OSSRH'
            url = uri('https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/')
            credentials {
                username project.findProperty('ossrhUsername') ?: System.getenv('OSSRH_USERNAME')
                password project.findProperty('ossrhPassword') ?: System.getenv('OSSRH_PASSWORD')
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey") ?: System.getenv('SIGNING_KEY')
    def signingPassword = findProperty("signingPassword") ?: System.getenv('OSSRH_PASSWORD')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
